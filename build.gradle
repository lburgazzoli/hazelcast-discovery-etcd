// *****************************************************************************
//
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'idea'
    id 'osgi'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'net.researchgate.release' version '2.3.4'
    //id 'com.github.ben-manes.versions' version '0.11.3'
    //id "com.github.nwillc.vplugin" version "1.2.5"
}

apply plugin: 'io.codearte.nexus-staging'
//apply from: "$rootDir/gradle/dependency-updates.gradle"

// *****************************************************************************
//
// *****************************************************************************

group   = 'com.github.lburgazzoli'

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    isCiBuild        = System.getenv('CI_TRAVIS') != null
    gitRoot          = "https://github.com/lburgazzoli"
    gitProject       = "https://github.com/lburgazzoli/hazelcast-discovery-etcd"
    gitURI           = "git@github.com/lburgazzoli/hazelcast-discovery-etcd.git"

    ext {
        versions = [
            slf4j              : '1.7.13',
            hazelcast          : '3.6',
            commonsLang        : '3.4',
            jackson            : '2.7.0',
            junit              : '4.12',
            etcd4j             : '2.8.0',
            groovy             : '2.4.5',
        ]
    }

    bundleImportPackages = [
        '*'
    ]

    bundleExportPackages = [
        "*"
    ]

    if (isCiBuild && isReleaseVersion) {
        // This will fail for release versions
        nexusUsername = ""
        nexusPassword = ""
    } else if (!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
        nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
        nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
    }
}

compileJava {
    // Suppress warnings about using Unsafe and sun.misc
    //options.compilerArgs << '-XDignore.symbol.file'
    //options.compilerArgs << '-parameters'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

// *************************************************************************
//
// *************************************************************************

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// *************************************************************************
//
// *************************************************************************

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile("org.slf4j:slf4j-api:$versions.slf4j") { transitive = false }
    runtime("org.slf4j:slf4j-ext:$versions.slf4j") { transitive = false }

    compile "org.apache.commons:commons-lang3:$versions.commonsLang"
    compile "com.hazelcast:hazelcast:$versions.hazelcast"
    compile "org.mousio:etcd4j:$versions.etcd4j"

    compile "com.fasterxml.jackson.core:jackson-core:$versions.jackson"
    compile "com.fasterxml.jackson.core:jackson-databind:$versions.jackson"
    compile "com.fasterxml.jackson.core:jackson-annotations:$versions.jackson"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:$versions.jackson"

    testCompile("org.slf4j:slf4j-simple:$versions.slf4j") { transitive = false }
    testCompile "org.codehaus.groovy:groovy-all:$versions.groovy:indy"
    testCompile "junit:junit:$versions.junit"
}

test {
    testLogging {
        events          'failed'
        exceptionFormat 'full'
    }
}

[compileJava, compileTestJava]*.options*.compilerArgs = [
    "-Xlint:unchecked",
    "-Xlint:deprecation",
]

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

// *************************************************************************
//
// *************************************************************************

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Bundle-Description'     ] = "${project.name}"
        attributes['Bundle-Name'            ] = "${project.name}"
        attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.name}"
        attributes['Bundle-Version'         ] = project.version.replaceAll('-','.')
        attributes['Implementation-Title'   ] = "${project.group}-${project.name}-${project.version}"
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'Luca Burgazzoli'

        instruction 'Import-Package' , bundleImportPackages.join(',')
        instruction 'Export-Package' , bundleExportPackages.join(',')
    }
}

// *************************************************************************
//
// *************************************************************************

nexus {
    sign = project.isReleaseVersion && !project.isCiBuild
}

extraArchive {
    javadoc = true
    sources = true
    tests   = false
}

modifyPom {
    project {
        name          = "${project.name}"
        description   = "${project.name}"
        url           = "${project.gitProject}"
        packaging     = 'jar'

        licenses {
            license {
                name         = "The Apache Software License, Version 2.0"
                url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "repo"
            }
        }

        scm {
            url                 = "${project.gitProject}"
            connection          = "scm:git:${project.gitURI}"
            developerConnection = "scm:git:${project.gitURI}"
        }

        developers {
            developer {
                id 'lburgazzoli'
                name 'Luca Burgazzoli'
                url gitRoot
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn uploadArchives
//closeAndPromoteRepository.dependsOn uploadArchives

task promote(dependsOn: closeAndPromoteRepository) {
    println "Release"
}
